Class {
	#name : 'Warshall',
	#superclass : 'Object',
	#instVars : [
		'adjacencyMatrix'
	],
	#category : 'Algorithms',
	#package : 'Algorithms'
}

{ #category : 'initialization' }
Warshall >> initialize [
    super initialize.
    adjacencyMatrix := #().
]

{ #category : 'initialization' }
Warshall >> setMatrix: aMatrix [
    "Set adjacency matrix for the algo"
    adjacencyMatrix := aMatrix.
]

{ #category : 'algorithm' }
Warshall >> findTransitiveClosure [
    "find transitive closure using Warshall's algo"
    | n result |
    n := adjacencyMatrix size.
    
    result := Array new: n.
    1 to: n do: [:i |
        result at: i put: ((adjacencyMatrix at: i) copy).
    ].
    
    1 to: n do: [:k |
        1 to: n do: [:i |
            1 to: n do: [:j |
                (result at: i) at: j put: 
                    ((result at: i) at: j) | 
                    (((result at: i) at: k) & ((result at: k) at: j)).
            ].
        ].
    ].
    
    ^ result
]

{ #category : 'printing' }
Warshall >> printMatrix: aMatrix [
    Transcript cr; show: 'Matrix:'; cr.
    aMatrix do: [ :row |
        Transcript show: row printString; cr.
    ].
]